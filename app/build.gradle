plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android-extensions'
    id 'androidx.navigation.safeargs.kotlin'
}

def credentialsProps = getCredentials()

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    //type
    def STRING = "String"
    def STRING_RES = "string"

    //variable
    def API_URL = "API_URL"
    def GOOGLE_MAP_KEY = "google_maps_key"

    flavorDimensions "api"

    defaultConfig {
        applicationId "com.imaginato.randomusers"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resValue STRING_RES, GOOGLE_MAP_KEY, credentialsProps['GOOGLE_MAP_KEY_VALUE']
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            dimension "api"
            buildConfigField STRING, API_URL, credentialsProps['APP_DEV_URL']
        }
        production {
            dimension "api"
            buildConfigField STRING, API_URL, credentialsProps['APP_PRODUCTION_URL']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding true
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'
    implementation 'com.google.android.gms:play-services-maps:17.0.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //Material
    implementation 'com.google.android.material:material:1.4.0-alpha02'

    //LiveData and ViewModel
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    //Logging Interceptor
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    //Hilt dependency injection
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.fragment:fragment-ktx:1.3.3"

    //Printing logs
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'

    //loading indicator animation library
    implementation 'com.airbnb.android:lottie:3.0.7'

    //For sp and dp
    implementation "com.intuit.ssp:ssp-android:1.0.6"
    implementation "com.intuit.sdp:sdp-android:1.0.6"

    //image loading library
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'
}

def getCredentials() {
    Properties credentialsProps = new Properties()
    def credentialsFile = file('app.properties')
    if (credentialsFile.canRead()) {
        credentialsProps.load(new FileInputStream(credentialsFile))
    }
    return credentialsProps
}